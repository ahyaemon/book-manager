plugins {
	id 'org.springframework.boot' version '2.1.4.RELEASE'
	id 'org.jetbrains.kotlin.jvm' version '1.2.71'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.2.71'
	id "org.jetbrains.kotlin.kapt" version "1.2.71"
	id "org.flywaydb.flyway" version "5.2.4"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
idea.module.inheritOutputDirs = true

group = 'com.iijima'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "org.springframework.boot:spring-boot-starter-quartz"
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	runtime 'org.postgresql:postgresql'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.1.1'
	annotationProcessor "org.seasar.doma:doma:2.24.0"
	implementation "org.seasar.doma:doma:2.24.0"
	kapt("org.seasar.doma:doma:2.24.0")
}

// コンパイルより前にSQL出力先ディレクトリにコピーするために依存関係を逆転する
processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources

kapt {
	arguments {
		arg("doma.resources.dir", compileKotlin.destinationDir)
	}
}

task copyDomaResources(type: Sync)  {
	from sourceSets.main.resources.srcDirs
	into compileKotlin.destinationDir
	include 'doma.compile.config'
	include 'META-INF/**/*.sql'
	include 'META-INF/**/*.script'
}

compileKotlin {
	dependsOn copyDomaResources
}
compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

flyway {
	url = 'jdbc:postgresql://localhost:5432/bookmanager'
	user = 'testman'
	password = 'testpass'
}
